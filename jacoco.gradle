// JaCoCo Plugin
// https://docs.gradle.org/current/userguide/jacoco_plugin.html
apply plugin: 'jacoco'

jacoco {
    reportsDir = file("${project.buildDir}/results")
    toolVersion = "0.8.5"
}

project.afterEvaluate {

    // collect all build types and product flavors
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }

    // when no product flavors are defined, add an empty one
    if (! productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->

            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"

            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
            task "${testTaskName}Coverage" (type: JacocoReport, dependsOn: "$testTaskName") {

                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."
                println("*** JaCoCo uses build/intermediates/javac/${buildTypeName}/classes")

                def classTree = fileTree(
                    dir: "${project.buildDir}/intermediates/javac/${buildTypeName}/classes",
                    excludes: [
                        '**/R.class',
                        '**/R$*.class',
                        '**/BR.*',
                        '**/*Binding.*',
                        '**/*BindingImpl.*',
                        '**/DataBindingInfo.class',
                        '**/DataBinderMapperImpl.class',
                        '**/DataBinderMapperImpl$*.class',
                        '**/databinding/*',
                        '**/*$ViewInjector*.*',
                        '**/*$ViewBinder*.*',
                        '**/BuildConfig.*',
                        '**/Manifest*.*'
                    ]
                )
                classDirectories.from = files([classTree])
                def coverageSourceDirs = ["src/main/java", "src/$buildTypeName/java"]
                executionData.from = file("${project.buildDir}/jacoco/${testTaskName}.exec")
                additionalSourceDirs.from = files(coverageSourceDirs)
                sourceDirectories.from = files(coverageSourceDirs)

                reports {
                    html.destination rootProject.file("results/jacoco")
                    html.enabled true
                    xml.enabled false
                    csv.enabled false
                }
            }
        }
    }
}
