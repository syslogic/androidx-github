// JaCoCo Plugin
// https://docs.gradle.org/current/userguide/jacoco_plugin.html
apply plugin: 'jacoco'

jacoco {
    reportsDir = file("getRootDir().path/results")
    toolVersion = "0.8.2"
}

project.afterEvaluate {

    def buildTypes = android.buildTypes.collect { type -> type.name }
    buildTypes.each { buildTypeName ->

        def sourceName = "${buildTypeName}"
        def sourcePath = "${buildTypeName}"
        def testTaskName = "test${sourceName.capitalize()}UnitTest"

        // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
        task "${testTaskName}Coverage" (type:JacocoReport, dependsOn: "$testTaskName") {

            group = "Reporting"
            description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

            classDirectories = fileTree(
                dir: "${project.buildDir}/javac/debug/compileDebugJavaWithJavac/classes/${sourcePath}",
                excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/*$ViewInjector*.*',
                    '**/*$ViewBinder*.*',
                    '**/BuildConfig.*',
                    '**/Manifest*.*'
                ]
            )

            def coverageSourceDirs = [
                "src/main/java",
                "src/$buildTypeName/java"
            ]
            additionalSourceDirs = files(coverageSourceDirs)
            sourceDirectories = files(coverageSourceDirs)
            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

            reports {
                xml.enabled false
                csv.enabled false
                html.enabled true
                html.destination file("${getRootDir().path}/results/jacoco")
            }
        }
    }
}