plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.androidx.room)
    id "maven-publish"
}

group = "io.syslogic"
version = libs.versions.app.version.name.get()

base {
    String versionName = libs.versions.app.version.name.get()
    archivesName = "androidx_github_$versionName"
}

android {
    namespace = "io.syslogic.github.api"
    buildToolsVersion = libs.versions.android.build.tools.get()
    compileSdk = Integer.parseInt(libs.versions.android.compile.sdk.get())
    defaultConfig {
        minSdk = Integer.parseInt(libs.versions.android.min.sdk.get())
        targetSdk = Integer.parseInt(libs.versions.android.target.sdk.get())
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFile("${project.rootDir}/proguard/consumer.pro")
    }

    sourceSets.main.java.srcDirs = [ "src/main/java" ]

    // Room Plugin
    room {
        schemaDirectory("${rootDir}/schema")
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
        dataBinding = true
    }

    buildTypes {
        debug {
            // it breaks the data-binding, eg. when running ./gradlew :library:publishToMavenLocal
            testCoverageEnabled = false
            minifyEnabled = false
        }
        release {
            minifyEnabled = false
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    lint {
        lintConfig = project.file("lint.xml")
        checkAllWarnings = true
        warningsAsErrors = true
        abortOnError = false
        showAll = false
    }
}

dependencies {

    // Material Design Components
    implementation(libs.material.design)

    // Annotations
    implementation(libs.androidx.annotation)

    // App Compat
    implementation(libs.androidx.appcompat)

    // Data-Binding Runtime
    implementation(libs.androidx.databinding)

    // Room Runtime
    annotationProcessor(libs.androidx.room.compiler)
    testImplementation(libs.androidx.room.testing)
    api(libs.androidx.room.runtime)

    // Retrofit2
    implementation(libs.gson)
    implementation(libs.retrofit)
    implementation(libs.retrofit.gson.converter) {
        exclude(group: "com.google.code.gson", module: "gson")
    }

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.junit)
}

tasks.register('javadoc', Javadoc) {

    title = "GitHub API Client ${android.defaultConfig.versionName}"
    source = android.sourceSets.main.java.srcDirs
    destinationDir = project.file("build/outputs/javadoc")
    configurations.implementation.setCanBeResolved(true)

    classpath = files(new File("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"))
    android.getBootClasspath().forEach{ classpath += fileTree(it) }
    classpath += fileTree(dir: project.file("build/tmp/aarsToJars/").absolutePath)
    classpath += configurations.implementation
    exclude("**/BuildConfig.java", "**/R.java", "**/*.kt")
    failOnError = false

    // options.verbose()
    // javadoc: warning - The code being documented uses modules but the packages
    // defined in https://developer.android.com/reference/ are in the unnamed module.
    options.links "https://docs.oracle.com/en/java/javase/17/docs/api/"
    options.linksOffline "https://developer.android.com/reference", "${android.sdkDirectory}/docs/reference"
    options.linkSource true
    options.author true

    onlyIf {
        project.file("build/intermediates/aar_main_jar").exists()
    }

    doFirst {

        // extract AAR files
        configurations.implementation
                .filter { it.name.endsWith('.aar') }
                .each { aar ->
                    copy {
                        from zipTree(aar)
                        include "**/classes.jar"
                        into project.file("build/tmp/aarsToJars/${aar.name.replace('.aar', '')}/")
                    }
        }

        // provide JAR, which contains the generated data-binding classes
        def aar_main = project.file("build/intermediates/aar_main_jar")
        if (aar_main.exists()) {
            copy {
                from aar_main
                include "**/classes.jar"
                into project.file("build/tmp/aarsToJars/aar_main_jar/")
            }
        }
    }
    doLast {
        // delete temporary directory.
        delete project.fileTree("build/tmp/aarsToJars")
    }
}

tasks.register("javadocJar", Jar) {
    dependsOn(javadoc)
    archiveClassifier.set("javadoc")
    from(javadoc.destinationDir)
}

tasks.register('sourcesJar', Jar) {
    from(android.sourceSets.main.java.srcDirs)
    archiveClassifier.set('sources')
}

artifacts {
    archives(javadocJar)
    archives(sourcesJar)
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = 'androidx-github'
                version = libs.versions.app.version.name.get()
                from components.getByName('release')
                pom {
                    name = 'GitHub API Client'
                    description = 'A simple client library for Android'
                    url = "https://github.com/syslogic/${artifactId}"
                    scm {
                        connection = "scm:git:git://github.com/syslogic/${artifactId}.git"
                        developerConnection = "scm:git:ssh://github.com/syslogic/${artifactId}.git"
                        url = "https://github.com/syslogic/${artifactId}/"
                    }
                }
            }
        }
    }
}
