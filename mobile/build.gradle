// :mobile build.gradle
plugins {
    id "com.android.application"
    id "androidx.navigation.safeargs"
}

android {
    compileSdk 34
    buildToolsVersion '34.0.0'
    defaultConfig {
        namespace rootProject.ext.get('applicationId')
        applicationId rootProject.ext.get('applicationId')
        versionName rootProject.ext.get('versionName')
        versionCode rootProject.ext.get('versionCode')
        manifestPlaceholders = [ accessToken: "" ]
        targetSdk 34
        minSdk 22
        setProperty("archivesBaseName", "github_client_" + versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testBuildType "debug"
        multiDexEnabled true
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$rootDir/schema".toString()]
            }
        }
    }

    sourceSets {
        main {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    testOptions {
        resultsDir = "${project.buildDir}/results"
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    signingConfigs {
        debug {

        }
        release {

        }
    }

    lint {
        lintConfig rootProject.file('lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [ accessToken: "${accessToken}" ]
            renderscriptDebuggable true
            pseudoLocalesEnabled false
            testCoverageEnabled true
            shrinkResources false
            zipAlignEnabled true
            minifyEnabled false
            jniDebuggable true
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [accessToken: ""]
            proguardFile "${project.rootDir}/proguard/android.pro"
            proguardFile "${project.rootDir}/proguard/androidx.pro"
            proguardFile "${project.rootDir}/proguard/retrofit.pro"
            proguardFile "${project.rootDir}/proguard/mobile.pro"
            testCoverageEnabled false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            pseudoLocalesEnabled false
            renderscriptDebuggable false
            jniDebuggable false
            debuggable false
        }
    }
}

dependencies {

    // Annotations
    implementation "androidx.annotation:annotation:$annotation_version"

    // Material Design
    implementation "com.google.android.material:material:$material_version"

    // Flexbox Layout
    implementation "com.google.android.flexbox:flexbox:$flexbox_version"

    // https://developer.android.com/jetpack/androidx/
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-splashscreen:$splash_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.preference:preference:$preference_version"
    implementation "androidx.cardview:cardview:$cardview_version"

    // Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"

    // Fragment
    androidTestImplementation "androidx.fragment:fragment-testing:$fragment_version"
    implementation "androidx.fragment:fragment:$fragment_version"

    // Room
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // Retrofit2
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation ("com.squareup.retrofit2:converter-gson:$retrofit_version") {
        exclude group: "com.google.code.gson", module: "gson"
    }

    // jUnit
    testImplementation "junit:junit:$junit_version"

    // https://mvnrepository.com/artifact/androidx.test.ext
    androidTestImplementation "androidx.test.ext:junit:1.1.5"

    // https://mvnrepository.com/artifact/androidx.test
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:rules:1.5.0"

    // https://mvnrepository.com/artifact/androidx.test.uiautomator/uiautomator
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiautomator_version"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espresso_version"
    // androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espresso_version"

    // The following dependency can be either "implementation" or "androidTestImplementation",
    // depending on whether you want it to appear on your APK's compile classpath or the test APK classpath.
    // androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"

}

// task :assemble finalized by :lint
tasks.whenTaskAdded { task ->
    if (task.name == "assembleDebug" || task.name == "bundleDebug") {
        task.finalizedBy "lintDebug"
    } else if (task.name == "assembleRelease" || task.name == "bundleRelease") {
        task.finalizedBy "lintRelease"
    }
}
