// :mobile build.gradle
apply plugin: "com.android.application"

android {

    defaultConfig {
        applicationId rootProject.ext.get('applicationId')
        manifestPlaceholders = [ accessToken: "" ]
        versionName rootProject.ext.get('versionName')
        versionCode rootProject.ext.get('versionCode')
        compileSdkVersion rootProject.ext.get('compileSdkVersion')
        targetSdkVersion rootProject.ext.get('targetSdkVersion')
        minSdkVersion rootProject.ext.get('minSdkVersion')
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testBuildType "debug"
        multiDexEnabled true

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
    }

    testOptions {
        resultsDir = "${project.buildDir}/results"
    }

    lintOptions {
        lintConfig rootProject.file('lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }

    dexOptions {
        javaMaxHeapSize '4572M'
        dexInProcess = true
    }

    signingConfigs {
        debug {

        }
        release {

        }
    }

    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [accessToken: "${accessToken}"]
            renderscriptDebuggable true
            pseudoLocalesEnabled false
            testCoverageEnabled true
            shrinkResources false
            zipAlignEnabled true
            minifyEnabled false
            jniDebuggable true
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [accessToken: "${accessToken}"]
            proguardFile "${project.rootDir}/proguard/android.pro"
            proguardFile "${project.rootDir}/proguard/androidx.pro"
            proguardFile "${project.rootDir}/proguard/retrofit.pro"
            proguardFile "${project.rootDir}/proguard/mobile.pro"
            testCoverageEnabled false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            pseudoLocalesEnabled false
            renderscriptDebuggable false
            jniDebuggable false
            debuggable false
        }
    }
}

dependencies {

    // https://developer.android.com/jetpack/androidx/
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.annotation:annotation:1.2.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.navigation:navigation-fragment:2.3.4'
    implementation 'androidx.navigation:navigation-ui:2.3.4'

    // https://mvnrepository.com/artifact/com.google.android.material/material
    implementation "com.google.android.material:material:1.3.0"

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation "com.squareup.okhttp3:okhttp:4.5.0"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation "com.google.code.gson:gson:2.8.6"

    // https://mvnrepository.com/artifact/com.squareup.retrofit2
    implementation ("com.squareup.retrofit2:retrofit:2.9.0") {
        exclude group: "com.squareup.okhttp3"
    }
    implementation('com.squareup.retrofit2:converter-gson:2.9.0') {
        exclude group: "com.google.code.gson"
    }

    // jUnit Tests
    testImplementation "junit:junit:4.13.2"

    // https://mvnrepository.com/artifact/androidx.test.ext
    androidTestImplementation "androidx.test.ext:junit:1.1.2"

    // https://mvnrepository.com/artifact/androidx.test
    androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"

    // https://mvnrepository.com/artifact/androidx.test.espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

    // https://mvnrepository.com/artifact/androidx.test.espresso/espresso-web
    androidTestImplementation "androidx.test.espresso:espresso-web:3.3.0"

    // https://mvnrepository.com/artifact/androidx.test.uiautomator/uiautomator
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"
}

// task :assemble finalized by :lint
tasks.whenTaskAdded { task ->
    if (task.name == "assembleDebug") {
        task.finalizedBy "lintDebug"
    } else if (task.name == "assembleRelease") {
        task.finalizedBy "lintRelease"
    }
}

