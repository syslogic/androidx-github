// :mobile build.gradle
plugins {
    id "com.android.application"
    id "androidx.navigation.safeargs"
}

android {
    namespace 'io.syslogic.github'
    buildToolsVersion = '34.0.0'
    compileSdk 34
    defaultConfig {
        minSdk 22
        targetSdk 34
        applicationId 'io.syslogic.github'
        manifestPlaceholders = [ accessToken: "" ]
        versionName rootProject.ext.get('version_name')
        versionCode rootProject.ext.get('version_code')
        setProperty("archivesBaseName", "github_client_$version_name")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testBuildType "debug"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    testOptions {
        resultsDir = "${project.buildDir}/results"
    }

    buildFeatures {
        buildConfig true
        dataBinding true
    }

    signingConfigs {
        debug {
            storeFile new File("${System.properties['user.home']}${File.separator}.android${File.separator}debug.keystore")
            if (rootProject.file('keystore.properties').exists()) {
                storePassword rootProject.ext.get('debugKeystorePass')
                keyAlias rootProject.ext.get('debugKeyAlias')
                keyPassword rootProject.ext.get('debugKeyPass')
            }
        }
        release {
            storeFile new File("${System.properties['user.home']}${File.separator}.android${File.separator}release.keystore")
            if (rootProject.file('keystore.properties').exists()) {
                storePassword rootProject.ext.get('releaseKeystorePass')
                keyAlias rootProject.ext.get('releaseKeyAlias')
                keyPassword rootProject.ext.get('releaseKeyPass')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [ accessToken: "${accessToken}" ]
            renderscriptDebuggable true
            pseudoLocalesEnabled false
            testCoverageEnabled true
            shrinkResources false
            minifyEnabled false
            jniDebuggable true
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [ accessToken: "" ]
            proguardFile "${project.rootDir}/proguard/android.pro"
            proguardFile "${project.rootDir}/proguard/androidx.pro"
            proguardFile "${project.rootDir}/proguard/retrofit.pro"
            proguardFile "${project.rootDir}/proguard/window.pro"
            proguardFile "${project.rootDir}/proguard/mobile.pro"
            proguardFile "${project.rootDir}/proguard/jgit.pro"
            testCoverageEnabled false
            minifyEnabled true
            shrinkResources true
            pseudoLocalesEnabled false
            renderscriptDebuggable false
            jniDebuggable false
            debuggable false
        }
    }

    lint {
        lintConfig project.file('lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }
}

dependencies {

    api project(path: ':library')

    // Annotations
    implementation "androidx.annotation:annotation:$annotation_version"

    // Material Design Components
    implementation "com.google.android.material:material:$material_version"

    // Flexbox Layout
    implementation "com.google.android.flexbox:flexbox:$flexbox_version"

    // https://developer.android.com/jetpack/androidx/
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-splashscreen:$splash_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.preference:preference:$preference_version"
    implementation "androidx.cardview:cardview:$cardview_version"

    // Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    implementation "androidx.navigation:navigation-fragment:$nav_version"

    // Fragment
    androidTestImplementation "androidx.fragment:fragment-testing:$fragment_version"
    implementation "androidx.fragment:fragment:$fragment_version"

    // Room Runtime
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // Retrofit2
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation ("com.squareup.retrofit2:converter-gson:$retrofit_version") {
        exclude group: "com.google.code.gson", module: "gson"
    }

    // https://mvnrepository.com/artifact/org.eclipse.jgit/org.eclipse.jgit
    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    // api "org.slf4j:slf4j-nop:$slf4j_version"
    api "org.slf4j:slf4j-simple:$slf4j_version"

    // jUnit
    testImplementation "junit:junit:$junit_version"

    // Required for connected tests.
    // https://mvnrepository.com/artifact/androidx.test/monitor
    debugImplementation "androidx.test:monitor:$test_monitor_version"

    // https://mvnrepository.com/artifact/androidx.test.ext
    androidTestImplementation "androidx.test.ext:junit:$test_junit_version"

    // https://mvnrepository.com/artifact/androidx.test
    // https://developer.android.com/jetpack/androidx/releases/test
    androidTestImplementation "androidx.test:core:$test_core_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test:rules:$test_rules_version"

    // https://mvnrepository.com/artifact/androidx.test.uiautomator/uiautomator
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiautomator_version"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espresso_version"
    // androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espresso_version"

    // The following dependency can be either "implementation" or "androidTestImplementation",
    // depending on whether you want it to appear on your APK's compile classpath or the test APK classpath.
    // androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"

}
