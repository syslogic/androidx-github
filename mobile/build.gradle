// :mobile build.gradle
apply plugin: "com.android.application"
apply plugin: "androidx.navigation.safeargs"

android {

    defaultConfig {
        applicationId rootProject.ext.get('applicationId')
        manifestPlaceholders = [ accessToken: "" ]
        versionName rootProject.ext.get('versionName')
        versionCode rootProject.ext.get('versionCode')
        compileSdkVersion rootProject.ext.get('compileSdkVersion')
        targetSdkVersion rootProject.ext.get('targetSdkVersion')
        minSdkVersion rootProject.ext.get('minSdkVersion')
        setProperty("archivesBaseName", "github_client_" + versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testBuildType "debug"
        multiDexEnabled true
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$rootDir/schema".toString()]
            }
        }
    }

    testOptions {
        resultsDir = "${project.buildDir}/results"
    }

    signingConfigs {
        debug {}
        release {}
    }

    sourceSets {
        main {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
    buildFeatures {
        dataBinding = true
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [accessToken: "${accessToken}"]
            renderscriptDebuggable true
            pseudoLocalesEnabled false
            testCoverageEnabled true
            shrinkResources false
            zipAlignEnabled true
            minifyEnabled false
            jniDebuggable true
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [accessToken: "${accessToken}"]
            proguardFile "${project.rootDir}/proguard/android.pro"
            proguardFile "${project.rootDir}/proguard/androidx.pro"
            proguardFile "${project.rootDir}/proguard/retrofit.pro"
            proguardFile "${project.rootDir}/proguard/mobile.pro"
            testCoverageEnabled false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            pseudoLocalesEnabled false
            renderscriptDebuggable false
            jniDebuggable false
            debuggable false
        }
    }
}

dependencies {

    // Annotations
    implementation "androidx.annotation:annotation:1.2.0"

    // Material Design
    implementation ("com.google.android.material:material:1.4.0") {
        exclude group: "androidx.core", module: "core"
        exclude group: "androidx.annotation", module: "annotation"
        exclude group: "androidx.appcompat", module: "appcompat"
        exclude group: "androidx.fragment", module: "fragment"
    }

    // https://developer.android.com/jetpack/androidx/
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.cardview:cardview:1.0.0"

    // Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:2.3.5"
    implementation ("androidx.navigation:navigation-fragment:2.3.5") {
        exclude group: "androidx.fragment", module: "fragment"
    }

    // Fragment
    androidTestImplementation "androidx.fragment:fragment-testing:1.3.6"
    implementation "androidx.fragment:fragment:1.3.6"

    // Room
    annotationProcessor "androidx.room:room-compiler:2.3.0"
    testImplementation "androidx.room:room-testing:2.3.0"
    implementation "androidx.room:room-runtime:2.3.0"

    // Retrofit2
    implementation "com.google.code.gson:gson:2.8.8"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation ("com.squareup.retrofit2:converter-gson:2.9.0") {
        exclude group: "com.google.code.gson", module: "gson"
    }

    // Kotlin (just to get rid of warnings)
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.30"

    // jUnit
    testImplementation "junit:junit:4.13.2"

    // https://mvnrepository.com/artifact/androidx.test.ext
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

    // https://mvnrepository.com/artifact/androidx.test
    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    // androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"
    // androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0"
    // androidTestImplementation "androidx.test.espresso:espresso-accessibility:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-web:3.4.0"
    // androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:3.4.0"

    // The following dependency can be either "implementation" or "androidTestImplementation",
    // depending on whether you want it to appear on your APK's compile classpath or the test APK classpath.
    // androidTestImplementation "androidx.test.espresso:espresso-idling-resource:3.4.0"

    // https://mvnrepository.com/artifact/androidx.test.uiautomator/uiautomator
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"
}

// task :assemble finalized by :lint
tasks.whenTaskAdded { task ->
    if (task.name == "assembleDebug") {
        task.finalizedBy "lintDebug"
    } else if (task.name == "assembleRelease") {
        task.finalizedBy "lintRelease"
    }
}
